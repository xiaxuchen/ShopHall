apply plugin: 'kotlin-kapt'

// 如果是单模块调试，则业务模块为application，否则为library
if (isDebug.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: rootDir.path + '/android.gradle'
android {
    defaultConfig {
        // 单模块调试下才有applicationId
        if (isDebug.toBoolean())
            applicationId "com.cxyz.main"
        kapt {
            arguments {
                arg("moduleName", project.getName())
            }
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (!isDebug.toBoolean()) {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            }
        }
    }

}

dependencies {
    // Anko Commons
    implementation "org.jetbrains.anko:anko-commons:${dep.ankoVersion}"

    // Anko Layouts
    implementation "org.jetbrains.anko:anko-sdk25:${dep.ankoVersion}" // sdk15, sdk19, sdk21, sdk23 are also available

    // Coroutine listeners for Anko Layouts
    implementation "org.jetbrains.anko:anko-sdk25-coroutines:${dep.ankoVersion}"

    implementation(project(':joint'))
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.annotation:annotation:1.0.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    testImplementation 'junit:junit:4.12'
    kapt "com.alibaba:arouter-compiler:${dep.routerAnnotationProcessor}"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
